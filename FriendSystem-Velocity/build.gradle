plugins {
    id 'java'
    id 'eclipse'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.0.1"
}

group = 'me.sk8ingduck'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()

    maven { url 'https://jitpack.io' }
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

dependencies {
    compileOnly 'net.luckperms:api:5.4'
    compileOnly "com.velocitypowered:velocity-api:3.0.1"
    compileOnly 'mysql:mysql-connector-java:8.0.33'
    annotationProcessor "com.velocitypowered:velocity-api:3.0.1"
    implementation group: 'com.zaxxer', name: 'HikariCP', version: '4.0.3'
    implementation "net.kyori:adventure-text-minimessage:4.13.1"
    implementation 'org.spongepowered:configurate-yaml:4.1.2'
}
compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

shadowJar {
    archiveClassifier.set('')
    mergeServiceFiles()
    configurations = [project.configurations.runtimeClasspath]
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

def templateSource = file('src/main/templates')
def templateDest = layout.buildDirectory.dir('generated/sources/templates')
def generateTemplates = tasks.register('generateTemplates', Copy) { task ->
    def props = [
            'version': project.version
    ]
    task.inputs.properties props

    task.from templateSource
    task.into templateDest
    task.expand props
}

sourceSets.main.java.srcDir(generateTemplates.map { it.outputs })

rootProject.idea.project.settings.taskTriggers.afterSync generateTemplates
project.eclipse.synchronizationTasks(generateTemplates)

// Ensure shadowJar is built with build task
build.dependsOn shadowJar